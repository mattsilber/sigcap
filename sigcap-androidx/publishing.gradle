Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

if (localProperties.getProperty("bintray.release_mode_androidx", "false") != "true") {
    return
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
    mavProjectName = "sigcap-androidx"
    mavLibraryDescription = 'Easily capture signatures (androidx / kotlin components)'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dokkaJavadoc.configure {
    outputDirectory.set(file("$buildDir/javadoc"))

    dokkaSourceSets {
        configureEach {
            includeNonPublic.set(false)
            skipDeprecated.set(false)
            reportUndocumented.set(true)
            skipEmptyPackages.set(true)
        }
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            sigcapAndroidxRelease(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocJar

                groupId = mavPublishGroupId
                artifactId = mavProjectName
                version = mavLibraryVersion

                pom {
                    name = mavProjectName
                    description = mavLibraryDescription
                    url = mavSiteUrl
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'mattsilber'
                            name = 'Matt Silber'
                            email = 'matt@guardanis.com'
                        }
                    }
                    scm {
                        url = mavGitUrl
                    }
                }
            }
        }
    }
}

bintray {
    user = localProperties.getProperty("bintray.user")
    key = localProperties.getProperty("bintray.apikey")

    publications = ['sigcapAndroidxRelease']
    configurations = ['archives']

    publish = true
    override = false

    pkg {
        repo = 'maven'
        name = mavProjectName
        desc = mavLibraryDescription
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = mavSiteUrl
        version {
            name = mavLibraryVersion
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = mavLibraryVersion
        }
    }
}
