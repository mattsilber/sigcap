Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

if (localProperties.getProperty("bintray.release_mode", "false") != "true") {
    return
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = mavLibraryVersion
group = mavPublishGroupId

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "mattsilber"
            name "Matt Silber"
            email "matt@guardanis.com"
        }
    }

    scm {
        connection mavGitUrl
        developerConnection mavGitUrl
        url mavSiteUrl
    }
}

afterEvaluate {
    publishing {
        publications {
            BintrayPublication(MavenPublication) {
                artifact "$buildDir/outputs/aar/$mavProjectName-release.aar"
//                from components.release
//                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocJar

                groupId mavPublishGroupId
                artifactId mavProjectName
                version mavLibraryVersion

                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', mavProjectName)
                    root.appendNode('description', mavLibraryDescription)
                    root.appendNode('url', mavSiteUrl)
                    root.children().last() + pomConfig

                    def dependenciesNode = root.getAt('dependencies')[0] ?: root.appendNode('dependencies')

                    configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = localProperties.getProperty("bintray.user")
    key = localProperties.getProperty("bintray.apikey")

    publications = ['BintrayPublication']
    configurations = ['archives']

    publish = true
    override = false

    pkg {
        repo = mavBintrayRepo
        name = mavProjectName
        description = mavLibraryDescription
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = mavSiteUrl
        version {
            name = mavLibraryVersion
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = mavLibraryVersion
        }
    }
}